apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: "$rootProject.projectDir/buildsystem/config.gradle"
apply from: "$rootProject.projectDir/buildsystem/version.gradle"
apply from: "$rootProject.projectDir/buildsystem/checkstyle.gradle"

def configuration = ext.configuration;

android {
  compileSdkVersion configuration.compileSdkVersion
  buildToolsVersion configuration.buildToolsVersion

  defaultConfig {
    minSdkVersion configuration.minimumSdkVersion
    targetSdkVersion configuration.targetSdkVersion

    applicationId configuration.applicationId

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/rxjava.properties'
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
    disable 'InvalidPackage'
    disable 'OldTargetApi'
  }

  testOptions {
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
  }

  productFlavors {
    mock {
      buildConfigField("boolean", "IS_MOCK", "true")
    }

    prod {
      buildConfigField("boolean", "IS_MOCK", "false")
    }
  }

  buildTypes {
    debug {
      debuggable true
      testCoverageEnabled = true
      signingConfig signingConfigs.debug
    }

    release {
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  signingConfigs {
    release {
      storeFile file("debug.keystore")
    }
  }

  android.variantFilter { variant ->
    def isRelease = variant.getBuildType().getName() == "release"
    def isMock = variant.getFlavors().get(0).getName() == "mock"

    if (isRelease && isMock) {
      variant.setIgnore(true)
    }
  }

  applicationVariants.all { variant ->
    def appName = configuration.applicationName
    def flavor = variant.mergedFlavor
    def isRelease = variant.getBuildType().getName() == "release"
    def isMock = variant.getFlavorName() == "mock"
    def isDebug = variant.getBuildType().getName() == "debug"

    if (isRelease) {
      flavor.versionCode = gitVersionCode
    } else {
      flavor.versionCode = gitVersionCodeTime
    }

    flavor.versionName = gitVersionName

    if (isMock) {
      flavor.applicationIdSuffix = ".mock"
      flavor.versionNameSuffix = "-mock"
      appName += "-mock"
    } else if (isDebug) {
      flavor.applicationIdSuffix = ".debug"
      flavor.versionNameSuffix = "-debug"
      appName += "-debug"
    }

    variant.resValue("string", "app_name", appName)

    variant.outputs.each { output ->
      def parentFile = output.outputFile.parent
      def fileName = output.outputFile.name.replace("presentation", "remotecraft")
      output.outputFile = new File(parentFile, fileName)
    }
  }
}

configurations.all {
  resolutionStrategy {
    force "${rootProject.ext.presentationDependencies.androidAnnotations}"
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

dependencies {
  def library = rootProject.ext.presentationDependencies;
  def libraryTest = rootProject.ext.presentationTestDependencies;

  compile project(':domain')
  compile project(':data')
  compile project(':infrastructure')

  compile "${library.timber}"
  apt "${library.daggerCompiler}"
  compile "${library.dagger}"
  compile "${library.rxJava}"
  compile "${library.rxAndroid}"
  provided "${library.javaxAnnotation}"
  compile "${library.lightweightStreamApi}"
  compile "${library.qrCodeReader}"
  provided "${library.autovalue}"
  apt "${library.autovalue}"
  apt "${library.autovalueParcel}"
  compile "${library.design}"
  compile "${library.appCompat}"
  compile "${library.recyclerView}"
  compile "${library.cardView}"
  compile "${library.androidAnnotations}"
  apt "${library.butterKnifeCompiler}"
  compile "${library.butterKnife}"
  compile "${library.picasso}"
  compile "${library.materialDesignColors}"
  compile "${library.androidSpinKit}"
  compile "${library.floatingActionButton}"
  compile "${library.kenBurnsView}"

  checkstyle "${library.checkstyle}"

  testCompile "${libraryTest.jUnit}"
  testCompile "${libraryTest.assertJ}"
  testCompile "${libraryTest.mockito}"
  testCompile "${libraryTest.robolectric}"
  testCompile "${libraryTest.openGlApi}"

  androidTestCompile "${libraryTest.espresso}"
  androidTestCompile "${libraryTest.conditionWatcher}"
}
